/*
 *  test_a2eif_cond_exp.sli
 *
 *  This file is part of NEST
 *
 *  Copyright (C) 2004 by
 *  The NEST Initiative
 *
 *  See the file AUTHORS for details.
 *
 *  Permission is granted to compile and modify
 *  this file for non-commercial use.
 *  See the file LICENSE for details.
 *
 */

/* BeginDocumentation
    Name: testsuite::test_a2eif_cond_exp - sli script for overall 
    test of a2eif_cond_exp model

    Synopsis: (test_a2eif_cond_exp.sli) run -> compares response to 
    synaptic input and current step with reference data 

    Description:
    test_a2eif_cond_exp.sli is an overall test of the a2eif_cond_exp model
    connected to some useful devices.

    Two spike generators as well as a DC current generator
    are connected to the neuron. One excitatory and one inhibitory spikes
    are emitted. A synaptic weight of 100 is used to cause a
    significant change of the membrane potential for the purpose of
    better scaling. Further a DC current is injected into the neuron.
    The membrane potential and the spiking activity are recorded by the
    corresponding devices.

    It can be observed how the incoming spikes and the DC current 
    change the membrane potential. Further a spike is emitted.

    Although 0.1 cannot be represented in the IEEE double data type, it
    is safe to simulate with a resolution (computation step size) of 0.1
    ms because by default nest is built with a timebase enabling exact
    representation of 0.1 ms.

    The expected output is documented and briefly commented at the end of
    the script. The textual output of the multimeter documented in this file
    can be regenerated by setting /to_screen true to the SetStatus
    call of mm below.

    Author:  March 2010 A. Kononov, T. Pfeil
*/


/unittest (8418) require
/unittest using
ResetKernel

0.1 /h Set

0 << 
      /local_num_threads 1 
      /resolution h
  >> SetStatus

/a2eif_cond_exp Create /neuron Set
neuron << 
         /V_peak      0.0 
         /V_reset    -60.0
         /t_ref       0.0
         /g_L         30.0
         /C_m         281.0
         /E_ex        0.0
         /E_in       -85.0
         /E_L        -70.6
         /Delta_T     2.0
         /V_th       -50.4
         /I_e         0.0
         /tau_syn_ex  0.2
         /tau_syn_in  2.0
         /a1          3.5
         /b1          70.5
         /a2          0.5
         /b2          10.0
         /tau_w1      144.0
         /tau_w2      500.0
       >> SetStatus

/spike_generator Create /sg_exc Set  %excitatory spike generator
sg_exc << 
         /spike_times  [5.0]
         /start         0.0
         /stop          100.0  
       >> SetStatus

/spike_generator Create /sg_inh Set  %inhibitory spike generator
sg_inh << 
         /spike_times  [35.0]
         /start         0.0
         /stop          100.0  
       >> SetStatus

/dc_generator Create /dc_gen Set
dc_gen << 
         /amplitude     700.0
         /start         80.0
         /stop          110.0  
       >> SetStatus

/multimeter Create /mm Set
mm <<
     /withtime       true
     /interval       h
     /record_from    [/V_m]
     /time_in_steps  true
   >> SetStatus

/spike_detector Create /sp_det Set
sp_det <<
         /withtime       true
         /time_in_steps  true
         /withgid        true
       >> SetStatus

sg_exc    neuron   100.0  h   Connect  % <-Setting synaptic weight 
sg_inh    neuron  -100.0  h   Connect  % <-to 100 for better scaling
dc_gen    neuron   1.0    h   Connect  %   of the resulting membrane
mm        neuron   1.0    h   Connect  %   potential
neuron    sp_det   1.0    h   Connect

150 Simulate


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% Expected output of this program:
% 
% The output send to std::cout is a superposition of the output of
% the multimeter and the spike detector. Both, multimeter and spike
% detector are connected to the same neuron. 
%

{
 sp_det [/events [/times]] get cva   	     % array of recorded data
 eq                                          % compare
}

%%%%%%%%%%
% spike times (in steps)
[[1047]] %there is only one spike

exch assert_or_die


{                                            % reference data
 dup Transpose First /test_times Set         % times of reference

 mm [/events [/times /V_m]] get cva          % array of recorded data
  6 ToUnitTestPrecision                      % to precision of reference
  Transpose                                  % all recorded tuples
  {First test_times exch MemberQ } Select    % those with reference
 eq                                          % compare
}

%%%%%%%%%%
% time (in steps)  membrane potential V_m (in mV)

[
[50	-70.6000]  % <--- incoming excitatory spike
[55	-66.5015]  % forces membrane potential to rise
[60	-66.1498]  % significantly due to the synaptic
[65	-66.3349]  % weight of 100
[70	-66.5531]  % V_m then decays towards
[80	-66.9633]  % resting potential
[90	-67.3326]  %
[100	-67.6648]  %
[120	-68.2324]  %
[140	-68.6916]  %
[160	-69.0630]  %
[180	-69.3635]  %
[200	-69.6066]  %
[220	-69.8031]  %
[250	-70.0298]  %
[350	-70.4245]  % <--- incoming inhibitory spike
[355	-72.1611]  % forces membrane potential to fall
[360	-73.5974]  % significantly due to synaptic
[365	-74.5100]  % weight of -100
[370	-75.0831]  % V_m then decays towards
[380	-75.6150]  % resting potential
[390	-75.6876]  %
[400	-75.5267]  %
[420	-74.9152]  %
[440	-74.2172]  %
[460	-73.5724]  %
[480	-73.0192]  %
[500	-72.5592]  %
[520	-72.1824]  %
[550	-71.7448]  %
[800	-70.6565]  % <--- dc generator is turned on,
[805	-69.6768]  % the membrane potential rises
[810	-68.5107]  %
[820	-66.3572]  %
[850	-61.1210]  %
[900	-55.3724]  %
[950	-51.8405]  %
[1000	-49.1039]  % <--- exponential threshold is reached
[1040	-44.8676]  % 
[1045	-41.7315]  %
[1046	-38.5844]  % <--- the spike is emitted
[1047	-59.9226]  %
[1055	-59.1274]  %
[1060	-58.6630]  %
[1080	-57.0284]  %
[1100	-55.6948]  % <--- dc generator is turned off
[1101	-55.6348]  % the membrane potential is pulled
[1102	-55.8233]  % towards the resting potential
[1200	-67.2612]  %
[1300	-71.2344]  %
[1400	-72.4881]  %
[1450	-72.7111]  %
]

exch assert_or_die

endusing
